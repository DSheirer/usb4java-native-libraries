plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = "io.github.dsheirer"
version = "1.3.1"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'usb4java-native-libraries'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'usb4java additional native libraries'
                description = 'Additional native libraries for usb4Java library'
                url = 'https://github.com/DSheirer/usb4java-native-libraries'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'dsheirer'
                        name = 'Dennis Sheirer'
                        email = 'dsheirer@sonatype.com'
                        organizationUrl = 'https://github.com/DSheirer'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/DSheirer/usb4java-native-libraries'
                    developerConnection = 'scm:git:ssh://github.com/DSheirer/usb4java-native-libraries.git'
                    url = 'https://github.com/DSheirer/usb4java-native-libraries/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar {
    manifest {
        attributes 'Implementation-Title': "${project.archivesBaseName}",
                'Implementation-Version': archiveVersion
    }
}
